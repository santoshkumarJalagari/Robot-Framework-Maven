*** Settings ***
Documentation     A resource file with reusable keywords and variables.
...
...               The system specific keywords created here form our own
...               domain specific language. They utilize keywords provided
...               by the imported Selenium2Library.
Library           Selenium2Library

*** Variables ***
${SERVER}         customer-stg.autodesk.com
${BROWSER}        Firefox
${DELAY}          0
${VALID USER}     userbicyaihccnhnh@ssttest.net
${VALID PASSWORD}    Password1
${LOGIN URL}      http://${SERVER}/
${WELCOME URL}    https://${SERVER}/cep/#products-services/all
${ERROR URL}      http://${SERVER}/error.html
${LoginPage TITLE}     Autodesk - Sign In
${HomePage TITLE}     Autodesk - Account

*** Keywords ***
Open Browser To Login Page
    Open Browser    ${LOGIN URL}    ${BROWSER}
    Maximize Browser Window
    Set Selenium Speed    ${DELAY}
    Login Page Should Be Open

Login Page Should Be Open
    Title Should Be    ${LoginPage TITLE}

Go To Login Page
    Go To    ${LOGIN URL}
    Login Page Should Be Open

Input Username
    [Arguments]    ${username}
    Input Text    xpath=.//*[@id='userName']    ${username}

Input Password
    [Arguments]    ${password}
    Input Text    xpath=.//*[@id='password']    ${password}

Submit Credentials
    Click Button    xpath=.//*[@id='btnSubmit']

Welcome Page Should Be Open
    Selenium2Library.Wait Until Element Is Visible    xpath=//h2[contains(text(),'All Products & Services')]    20000
    Location Should Be    ${WELCOME URL}
    Title Should Be    ${HomePage TITLE}
